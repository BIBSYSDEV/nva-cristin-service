plugins {
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco-report-aggregation'
}

group 'no.unit.nva'
version '1.0-JDK17'
sourceCompatibility = JavaVersion.VERSION_17  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_17 // bytecode target version

dependencies {
    implementation libs.bundles.nvaCommons
    testImplementation libs.bundles.testing
    implementation libs.bundles.jackson
    implementation libs.slf4j.api
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

pmd {
    toolVersion = '6.20.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

test {
    println("Test config for ${project.name}")
    useJUnitPlatform()
//    failFast = true
    testLogging {
        events 'failed'
    }
    environment "ALLOWED_ORIGIN", "*"
    environment "CRISTIN_API_URL", "https://api.cristin-test.uio.no/v2"
    environment "BASE_PATH", "cristin"
    environment "DOMAIN_NAME", "api.dev.nva.aws.unit.no"
    finalizedBy jacocoTestReport
}

tasks.withType(Checkstyle).configureEach{
    println("Checkstyle for ${project.name}")
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}


//tasks.named('check') {
//    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
//    dependsOn tasks.named('jacocoTestCoverageVerification', JacocoReport)
////    dependsOn tasks.named('integrationTestCodeCoverageReport', JacocoReport)
//}



jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.800
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 0.900
            }
        }
    }
    dependsOn jacocoTestReport
}


tasks.register('karateTest', Test) {
    useJUnitPlatform {
        includeTags('karateTest')
    }

    environment "ALLOWED_ORIGIN", "*"
    environment "CRISTIN_API_URL", "https://api.cristin-test.uio.no/v2"
    environment "DOMAIN_NAME", "api.dev.nva.aws.unit.no"

    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    outputs.upToDateWhen { false }
}

tasks.register('createTestUsers', Test) {
    useJUnitPlatform {
        includeTags('createTestUsers')
    }

    outputs.upToDateWhen { false }
}

tasks.register('deleteTestUsers', Test) {
    useJUnitPlatform {
        includeTags('deleteTestUsers')
    }

    outputs.upToDateWhen { false }
}




