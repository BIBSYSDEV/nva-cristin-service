plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'base'
}

group 'no.unit.nva'
version '1.0-SNAPSHOT'

dependencies {
    testImplementation libs.bundles.testing
    implementation libs.bundles.nvaCommons
    implementation libs.bundles.jackson
    implementation libs.slf4j.api
}

sourceCompatibility = JavaVersion.VERSION_11  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_11 // bytecode target version

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

compileJava.options.encoding = 'utf-8'

tasks.withType(JavaCompile) {
    options.encoding = 'utf-8'
}

tasks.named('test') {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'skipped', 'passed', 'failed'
    }
    finalizedBy jacocoTestReport
}

test {
    environment "ALLOWED_ORIGIN", "*"
    environment "CRISTIN_API_URL", "https://api.cristin-test.uio.no/v2"
    environment "BASE_PATH", "cristin"
    environment "DOMAIN_NAME", "api.dev.nva.aws.unit.no"
}

task karateTest(type: Test) {
    useJUnitPlatform {
        includeTags('karateTest')
    }

    environment "ALLOWED_ORIGIN", "*"
    environment "CRISTIN_API_URL", "https://api.cristin-test.uio.no/v2"
    environment "DOMAIN_NAME", "api.dev.nva.aws.unit.no"

    systemProperty "karate.options", System.properties.getProperty("karate.options")
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    outputs.upToDateWhen { false }
}

task createTestUsers(type: Test) {
    useJUnitPlatform {
        includeTags('createTestUsers')
    }

    outputs.upToDateWhen { false }
}

task deleteTestUsers(type: Test) {
    useJUnitPlatform {
        includeTags('deleteTestUsers')
    }

    outputs.upToDateWhen { false }
}


pmd {
    toolVersion = '6.20.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            excludes = [
                    'cristin-commons:*'
            ]
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }

        rule {
            excludes = [
                    'cristin-commons:*'
            ]
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}
